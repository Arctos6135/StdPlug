plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'

    // Apply the javafxplugin for JavaFX classes 
    id 'org.openjfx.javafxplugin' version '0.0.7'
}

project.version = "0.1.0"

// Set up JavaFX plugin
javafx {
    version = "12.0.1"
    modules = [ 'javafx.graphics', 'javafx.fxml', 'javafx.controls' ]
}

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()

    // FRC Maven Server
    // For the Shuffleboard plugins framework
    maven {
        url "https://first.wpi.edu/FRC/roborio/maven/release/"
    }
}

dependencies {
    // Shuffleboard plugins framework
    implementation(group: "edu.wpi.first.shuffleboard", name: "api", version: "2019.4.1")

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

// Add sources to the jar and exclude API classes
jar {
    from sourceSets.main.allSource
    
    archiveName 'StdPlug-' + project.version + '.jar'
}

task apiJar(type: Jar, group: 'Build', description: 'Generates a jar of the StdPlug robot-side API.') {
    dependsOn classes

    archiveName 'StdPlug-API-' + project.version + '.jar'

    from(sourceSets.main.output) {
        include 'com/arctos6135/stdplug/api/**'
    }
    
    from(sourceSets.main.allSource) {
        include 'com/arctos6135/stdplug/api/**'
    }
}

build {
    dependsOn apiJar
}
